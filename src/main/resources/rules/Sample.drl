package com.sample

import com.sample.Vacina.*;
import com.sample.Eventos.*;
import java.util.Date;

declare MudancaTemperatura
	@role( event )
	@timestamp( inicio )
end

/*
rule "evento de mudança de temperatura"
when
	$mudanca : MudancaTemperatura()
	$gerente : Gerente()
then
	System.out.println($gerente + " " + $mudanca);
end
*/

/* ================================================= Regras para verificar perigo ================================================= 
	Caso a temperatura de uma câmara de vacina chegue perto dos limites dos
		parâmetros, deve-se notificar os gestores. */

declare Perigo
	@role( event )
	@timestamp( inicio )
	@duration( duration )
end

rule "temperatura perto dos limites"
when
	$mudanca : MudancaTemperatura( $camara : camara, $temp : temp, ($temp - $camara.getVacina().getTipo().getTempMin() < 1 && > 0 ) || ($camara.getVacina().getTipo().getTempMax() - $temp < 1 && > 0) ) over window:length(1)
	not (exists ( Perigo( $camara == camara, ativo == true) ) )
then
	insert(new Perigo( $camara, $temp, true, new Date()) );
	$camara.sendMessage("<<< A Câmara " + $camara.getObjectId() + " está com temperatura " + $temp + " muito perto dos limites. >>>"); 
end

rule "fora de perigo"
when
	$mudanca : MudancaTemperatura( $camara : camara, $temp : temp, ($temp - $camara.getVacina().getTipo().getTempMin() > 1) && ($camara.getVacina().getTipo().getTempMin() - $temp > 1) ) over window:length(1)
	$perigo : Perigo( $camara == camara, $inicio : inicio, ativo == true)
then
	$perigo.setAtivo( false );
	$perigo.setDuration( System.currentTimeMillis() - $inicio.getTime() );
	update( $perigo );
	$camara.sendMessage("\n\t\t<<< A Câmara " + $camara.getObjectId() + " está com temperatura " + $temp + " fora de perigo. >>>");
end

/* ================================================= Regras para verificar temperatura fora da faixa apropriada ================================================= 
	Caso a temperatura de uma câmara de vacina passe dos limites dos
		parâmetros, deve-se chamar o gestor mais próximo ao local. */
		
declare Alerta
	@role( event )
	@timestamp( inicio )
	@duration( duration )
end

rule "temperatura fora do apropriado"
when
	$mudanca : MudancaTemperatura( $camara : camara, $temp : temp, ( $temp < $camara.getVacina().getTipo().getTempMin() ) || ( $temp > $camara.getVacina().getTipo().getTempMax() ) ) over window:length(1)
	not ( exists ( Alerta( $camara == camara, ativo == true ) ) )
then
	insert ( new Alerta($camara, $camara.gerenteMaisProx(), $temp, true, new Date()) );
	$camara.gerenteMaisProx().sendMensagem(	"\n\t\t<<< Alerta na Câmara " + $camara.getObjectId() + " está com temperatura " + $temp + 
							"\n\t\tFavor comparecer no local " + $camara.getLocal() + " imediatamente. >>> ");
end

rule "temperatura apropriada"
when
	$mudanca : MudancaTemperatura( $camara : camara, $temp : temp, ( $temp > $camara.getVacina().getTipo().getTempMin() ) && ( $temp < $camara.getVacina().getTipo().getTempMax() ) ) over window:length(1)
	$alerta : Alerta( $camara == camara, $inicio : inicio, $gerente : gerente, ativo == true)
then
	$alerta.setAtivo(false);
	$alerta.setDuration( System.currentTimeMillis() - $inicio.getTime() );
	update( $alerta );
	$gerente.sendMensagem("<<< A Câmara " + $camara.getObjectId() + " está com temperatura " + $temp + " fora de alerta. >>>");
end